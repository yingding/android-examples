/*
 * Copyright 2021 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    // Define versions in a single place
    ext {
        // default application config, Sdk and tools
        compileSdkVersion = 32
        buildToolsVersion = "32.0.0"
        applicationId = "com.example.compose.rally"
        minSdkVersion = 21
        targetSdkVersion = 32
        versionCode = 1
        versionName = "1.0"

        // sdk
        ktlintVersion = '0.42.1'
        // Testing
        junitVersion = '4.13.1'
        testExtJunit = '1.1.3'
        espressoVersion = '3.4.0'
        truthVersion = '1.0.1'
        runnerVersion = '1.0.1'
        coroutinesVersion = "1.6.0" // "1.5.2"
        coreTestingVersion = '2.1.0'

        // App dependencies
        appCompatVersion = '1.4.1'
        fragmentVersion = '1.4.1' // '1.3.6'

        activityComposeVersion = '1.4.0'
        composeVersion = '1.1.1'

        // androidx.core
        ktxVersion = '1.7.0'
        lifecycleVersion = '2.3.0'
        materialVersion = '1.5.0' //'1.4.0'

        uiAutomatorVersion = '2.2.0'
        viewPagerVersion = '1.0.0'

        // Android Studio
        kotlinVersion = '1.6.10'
        gradleVersion = '7.1.3'
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.10.0'
}

subprojects {
    repositories {
        google()
        mavenCentral()
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint(rootProject.ktlintVersion)
            licenseHeaderFile rootProject.file('spotless/copyright.kt')
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = true

            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'

            // Enable experimental coroutines APIs, including Flow
            freeCompilerArgs += '-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi'
            freeCompilerArgs += '-Xopt-in=kotlinx.coroutines.FlowPreview'
            freeCompilerArgs += '-Xopt-in=kotlin.Experimental'

            // Set JVM target to 1.8
            jvmTarget = "1.8"
        }
    }
}
